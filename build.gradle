version = '0.2.0'

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
}

['java', 'distribution', 'maven-publish'].each {
    apply plugin: it
}
import org.gradle.util.VersionNumber

String binedVersion = '0.2.1-SNAPSHOT';

sourceCompatibility = '1.8'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    doFirst {
        options.fork = true
        if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
            if (VersionNumber.parse(gradle.gradleVersion) >= VersionNumber.parse('5.0')) {
                options.bootstrapClasspath = files("${System.properties['java.home']}/lib/rt.jar", "$File.pathSeparator${System.properties['java.home']}/jre/lib/jsse.jar")
            } else {
                options.bootClasspath = "${System.properties['java.home']}/lib/rt.jar"
                options.bootClasspath += "$File.pathSeparator${System.properties['java.home']}/jre/lib/jsse.jar"
            }
        }
    }
}

// Disable tests on build
if (!gradle.startParameter.taskNames.any {it.endsWith("test")}) {
    tasks.withType(Test) {enabled = false}
}

task testJar(type: Jar, dependsOn: testClasses) {
    classifier "tests"
    archiveBaseName = "test-${project.archivesBaseName}"
    from sourceSets.test.output
}

configurations {
    tests
}

artifacts {
    tests testJar
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java
}

javadoc {
    if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
        options.addBooleanOption('html5', true)
    }
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.exbin.bined.jedit.NoMainClass'
}

repositories {
    mavenCentral()
    flatDir {
        dirs "lib"
    }
}

dependencies {
    implementation ':jedit'

    implementation ":bined-core-${binedVersion}"
    implementation ":bined-extended-${binedVersion}"
    implementation ":bined-highlight-swing-${binedVersion}"
    implementation ":bined-operation-${binedVersion}"
    implementation ":bined-operation-swing-${binedVersion}"
    implementation ":bined-swing-${binedVersion}"
    implementation ":bined-swing-extended-${binedVersion}"
    implementation ":paged_data-0.2.1-SNAPSHOT"
    implementation ":paged_data-delta-0.2.1-SNAPSHOT"
    implementation ':jsr305-3.0.2'
    testImplementation group : 'junit', name: 'junit', version: '4.10'
}

jar {
    manifest {
        attributes(
            'Main-Class': mainClass
        )
    }

    from configurations.compileClasspath.findAll { it.name != 'jedit.jar' }.collect { it.isDirectory() ? it : zipTree(it) }
    destinationDirectory = file("$buildDir")
}

distZip.dependsOn jar
distTar.dependsOn jar

distributions {
    main {
        contents {
            from 'src/dist'
            include 'readme.txt'

            into('') {
                from project.jar.archivePath
                include '*'
                rename { filename -> "BinEdPlugin.jar" }
            }
        }
    }
}
